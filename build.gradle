import java.awt.Desktop

apply plugin: 'groovy'
apply plugin: 'application'

project.ext {
  testFailed = ''
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.7'
    compile group: 'org.slf4j', name:'slf4j-api', version: '1.6.4'
    compile 'org.apache.ant:ant:1.7.1'

    testCompile group: 'junit', name: 'junit', version: '4.8.2'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'cglib:cglib-nodep:2.2.2'
}

test {

    testLogging.showStandardStreams = true

    afterTest { desc, result ->
        if (result.toString() == 'FAILURE') {
            project.testFailed = "${desc.className}"
        }
    }
}

gradle.taskGraph.afterTask { task, taskState ->
    if (testTaskFailed(task)) {
        openFailedTestReportInBrowser(task)
    }
}

def openFailedTestReportInBrowser(task) {
    if (Desktop.isDesktopSupported()) {
        Desktop.desktop.browse(new File(task.reports.html.destination, "index.html").toURI())
    }
}

def testTaskFailed(task) {
    task instanceof Test && !project.testFailed.isEmpty()
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bluepapa32:gradle-watch-plugin:0.1.3'
    }
}

apply plugin: 'watch'

watch {

    java {
        files files('src/main/java')
        tasks 'compileJava'
    }
}